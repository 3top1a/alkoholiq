// grammar.pest
WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT = _{ ("//"|"#") ~ (!"\n" ~ ANY)* }

program = { SOI ~ (NEWLINE | (instruction ~ NEWLINE) | instruction )* ~ EOI }

instruction = !{
    copy_instr |
    inc_instr |
    dec_instr |
    inc_by_instr |
    dec_by_instr |
    set_instr |
    read_instr |
    printc_instr |
    print_instr |
    print_msg_instr |
    match_instr |
    case_instr |
    add_instr |
    sub_instr |
    mul_instr |
    div_instr |
    if_equal_instr |
    if_equal_const_instr |
    if_not_equal_instr |
    if_not_equal_const_instr |
    until_equal_instr |
    while_not_zero_instr |
    compare_instr |
    push_instr |
    pop_instr |
    raw_instr |
    end_instr
}

// Basic types
variable = @{ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "-" | "_")* }
immediate = @{ ASCII_DIGIT+ }
string_literal = @{ ("\"" ~ (!"\"" ~ ANY)* ~ "\"") | ("'" ~ (!"\'" ~ ANY)* ~ "'") }

// Instructions
copy_instr = { "copy" ~ variable ~ variable }
inc_instr = { "inc" ~ variable }
dec_instr = { "dec" ~ variable }
inc_by_instr = { "inc_by" ~ variable ~ immediate }
dec_by_instr = { "dec_by" ~ variable ~ immediate }
set_instr = { "set" ~ variable ~ immediate }
read_instr = { "read" ~ variable }
printc_instr = { "printc" ~ variable }
print_instr = { "print" ~ variable }
print_msg_instr = { "print_msg" ~ string_literal }
match_instr = { "match" ~ variable ~ immediate+ }
case_instr = { "case" }
add_instr = { "add" ~ variable ~ variable }
sub_instr = { "sub" ~ variable ~ variable }
mul_instr = { "mul" ~ variable ~ variable }
div_instr = { "div" ~ variable ~ variable ~ variable ~ variable }
if_equal_instr = { "if_eq" ~ variable ~ variable }
if_equal_const_instr = { "if_eq" ~ variable ~ immediate }
if_not_equal_instr = { "if_neq" ~ variable ~ variable }
if_not_equal_const_instr = { "if_neq" ~ variable ~ immediate }
until_equal_instr = { "until_eq" ~ variable ~ variable }
while_not_zero_instr = { "while_nz" ~ variable }
compare_instr = { "compare" ~ variable ~ variable ~ variable }
push_instr = { "push" ~ variable }
pop_instr = { "pop" ~ variable }
raw_instr = { "raw" ~ string_literal }
end_instr = { "end" }
